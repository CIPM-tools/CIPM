
import "http://www.xtext.org/lua/Lua" as lua
import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm

reactions: luaComponent_operationProvidedRole
in reaction to changes in lua
execute actions in pcm

routine createOperationProvidedRoleForLuaComponent(lua::Component luaComponent) {
	match {
		require absence of pcm::OperationProvidedRole corresponding to luaComponent
		val basicComponent = retrieve pcm::BasicComponent corresponding to luaComponent
		val operationInterface = retrieve pcm::OperationInterface corresponding to luaComponent
	}
	create {
		val operationProvidedRole = new pcm::OperationProvidedRole
	}
	update {
		operationProvidedRole.providedInterface__OperationProvidedRole = operationInterface
		operationProvidedRole.providingEntity_ProvidedRole = basicComponent
		operationProvidedRole.entityName = luaComponent.name + "_providing_role"

		basicComponent.providedRoles_InterfaceProvidingEntity += operationProvidedRole

		addCorrespondenceBetween(operationProvidedRole, luaComponent)
	}
}

routine removeOperationProvidedRoleForLuaComponent(lua::Component luaComponent) {
	match {
		val operationProvidedRole = retrieve pcm::OperationProvidedRole corresponding to luaComponent
		val basicComponent = retrieve pcm::BasicComponent corresponding to luaComponent
	}
	update {
		removeCorrespondenceBetween(luaComponent, operationProvidedRole)
		basicComponent.providedRoles_InterfaceProvidingEntity -= operationProvidedRole
		operationProvidedRole.removeObject
	}
}